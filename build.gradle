/*
 * Copyright 2011-2019 The OTP authors
 *
 * Permission is hereby granted, free of charge, to any person obtaining a copy
 * of this software and associated documentation files (the "Software"), to deal
 * in the Software without restriction, including without limitation the rights
 * to use, copy, modify, merge, publish, distribute, sublicense, and/or sell
 * copies of the Software, and to permit persons to whom the Software is
 * furnished to do so, subject to the following conditions:
 *
 * The above copyright notice and this permission notice shall be included in all
 * copies or substantial portions of the Software.
 *
 * THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
 * IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
 * FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE
 * AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
 * LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM,
 * OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN THE
 * SOFTWARE.
 */

import com.gradle.scan.plugin.BuildScanPlugin
import com.gradle.scan.plugin.PublishedBuildScan
import grails.util.Environment

import static java.util.concurrent.TimeUnit.SECONDS

buildscript {
    repositories {
        mavenLocal()
        mavenCentral()
        maven { url "https://repo.grails.org/grails/core" }
    }
    dependencies {
        classpath "org.grails:grails-gradle-plugin:$grailsVersion"
        classpath "org.grails.plugins:hibernate5:7.0.6"
        classpath "com.bertramlabs.plugins:asset-pipeline-gradle:3.0.11"
        classpath "com.bertramlabs.plugins:less-asset-pipeline:3.0.11"
        classpath 'org.grails.plugins:database-migration:3.1.0'
    }
}

plugins {
    id "com.github.monosoul.markdown.page.generator" version "2.1.0.1"
    id "com.gradle.build-scan" version "2.0.2"
    id 'org.hidetake.swagger.generator' version '2.18.1'
    id "com.github.node-gradle.node" version "2.2.4"
}

buildScan {
    termsOfServiceUrl = 'https://gradle.com/terms-of-service'
    termsOfServiceAgree = 'yes'
    if (System.getenv('PUBLISH_SCAN') == '1') {
        publishAlways()
        String commitId = 'git rev-parse --verify HEAD'.execute().text.trim()
        String gitlabUrl = "https://gitlab.com/one-touch-pipeline/otp"
        value 'Git Commit ID', commitId
        buildScan.link 'Source', "$gitlabUrl/-/commit/$commitId"
        buildScanPublished { PublishedBuildScan scan ->
            file("$buildDir/scan-journal.log") << "${new Date()} - ${scan.buildScanId} - ${scan.buildScanUri}\n"
        }
    }
}

group "de.dkfz.tbi"

apply plugin: "idea"
apply plugin: "war"
apply plugin: "org.grails.grails-web"
apply plugin: "asset-pipeline"
apply plugin: "org.grails.grails-gsp"
apply plugin: "codenarc"
apply plugin: BuildScanPlugin

repositories {
    mavenLocal()
    mavenCentral()
    maven { url "https://repo.grails.org/grails/core" }
    maven { url "https://jitpack.io" }
}

sourceSets {
    testHelper {
        groovy {
            srcDirs {
                "src/test-helper/groovy"
            }
        }
        compileClasspath += main.runtimeClasspath
    }
    test {
        compileClasspath += sourceSets.testHelper.output
        runtimeClasspath += sourceSets.testHelper.output
    }

    workflowTest {
        groovy {
            srcDirs {
                "src/workflow-test/groovy"
            }
        }
        compileClasspath += main.runtimeClasspath + testHelper.output
        runtimeClasspath += main.runtimeClasspath + testHelper.output

        File r = new File(project.projectDir, "grails-app/conf")
        resources.srcDir(r)
    }
    main {
        resources {
            srcDirs 'migrations', "$buildDir/version"
        }
    }

    script {
        groovy {
            srcDirs {
                "scripts"
            }
        }
        compileClasspath += main.runtimeClasspath
    }
}

swaggerSources {
    wes {
        inputFile = file("${sourceSets.main.resources.srcDirs[0]}/wes-api/workflow_execution_service.swagger.yaml")
        code {
            language = 'java'
            configFile = file("${sourceSets.main.resources.srcDirs[0]}/wes-api/workflow_execution_service.swagger.config.json")
            dependsOn validation
        }
    }
}
compileJava.dependsOn swaggerSources.wes.code
sourceSets.main.java.srcDir "${swaggerSources.wes.code.outputDir}/src/main/java"
sourceSets.main.resources.srcDir "${swaggerSources.wes.code.outputDir}/src/main/resources"

integrationTest {
    boolean usePostgresDocker = "TRUE".equalsIgnoreCase(System.getenv('USE_POSTGRES'))
    systemProperty "usePostgresDocker", usePostgresDocker.toString()

    if (usePostgresDocker) {
        List<String> dcs = []
        doFirst {
            println "use postgres docker"

            // Where is docker-compose?
            String dockerCompose = '/usr/bin/which docker-compose'.execute()?.text?.trim()
            if (!dockerCompose) {
                throw new IllegalStateException('Unable to find docker-compose. Is it installed and available via PATH?')
            }

            List<String> dockerPorts = (0..maxParallelForks - 1).collect {

                // Setup the project name for use by the Continuous Integration server the prevent race conditions.
                String projectName = "--project-name otptest-build-${System.getenv('BUILD_NUMBER') ?: 'DEFAULT'}-w-${it}"

                // Construct command string
                String dc = "${dockerCompose} ${projectName} --file docker/otp-test/docker-compose.yml"
                dcs << dc

                //cleanup in case the container still exist
                "${dc} stop".execute().waitFor()
                "${dc} rm -f".execute().waitFor()

                // Start a fresh container
                if ("${dc} up -d".execute().waitFor() != 0) {
                    throw new IllegalStateException('Unable to start new database container.')
                }
                // Get the (dynamically assigned local) port
                def dockerProcess = "${dc} port integration 5432".execute()
                // If we do not get a reply in time or the return code indicates a failure, fail.
                if (!dockerProcess.waitFor(5, SECONDS) || dockerProcess.exitValue() != 0) {
                    throw new IllegalStateException('Unable to get port information from Docker. Is the test database container running?')
                }
                String dockerPort = dockerProcess.text.split(':').last().trim() // should work for IPv6 also
                return dockerPort
            }

            systemProperty "maxParallelForks", maxParallelForks
            systemProperty "dockerPorts", dockerPorts.join(',')
        }

        doLast {
            // Stop and destroy the container
            dcs.each { dc ->
                "${dc} stop".execute().waitFor()
                "${dc} rm -f".execute().waitFor()
            }
        }
    } else {
        doFirst {
            println "use h2 database"
        }
    }
}

task workflowTest(type: Test) {
    testClassesDirs = sourceSets.workflowTest.output.classesDirs
    classpath = sourceSets.workflowTest.runtimeClasspath
    reports.html.enabled = true
    testLogging.showStandardStreams = true
    group = LifecycleBasePlugin.VERIFICATION_GROUP
    shouldRunAfter "integrationTest"
    systemProperty "grails.env", "WORKFLOW_TEST" // change to TEST??
}

configurations {
    developmentOnly
    runtimeClasspath {
        extendsFrom developmentOnly
    }

    all {
        exclude group: 'org.webjars', module: 'popper.js'    // exclude popper.js, since the correct version is already imported within bootstrap.bundle
    }
}

dependencies {
    swaggerCodegen 'io.swagger:swagger-codegen-cli:2.4.3' // swagger 2.x codegen
    implementation 'io.swagger:swagger-annotations:1.6.0'

    compile group: 'commons-io', name: 'commons-io', version: '2.11.0'

    integrationTestCompile sourceSets.testHelper.output
    integrationTestRuntime sourceSets.testHelper.output

    // JDBC
    runtime 'org.postgresql:postgresql:42.3.3'

    // Dicom Audit Logging
    if (Environment.isDevelopmentMode()) {
        compile "com.gitlab.one-touch-pipeline:dicom-audit-trail-impl:v1.1.0-NOP"
    } else {
        compile "com.gitlab.one-touch-pipeline:dicom-audit-trail-impl:1.1.0"
    }
    compile "com.gitlab.one-touch-pipeline:dicom-audit-trail:1.2.0"

    compile "com.google.code.gson:gson:2.8.9"

    // bioinformatics tools
    compile "com.gitlab.one-touch-pipeline:bedutils:0.0.8"
    compile "com.github.samtools:htsjdk:2.24.1"

    // general dependencies
    compile "org.jadira.usertype:usertype.core:7.0.0.CR1"
    compile "com.opencsv:opencsv:4.4"

    compile "org.grails:grails-async-gpars:3.3.2"

    // library for interacting with cluster job schedulers
    compile('com.github.theroddywms:BatchEuphoria:0.0.14') {
        exclude group: 'org.apache.commons', module: 'commons-io'
    }

    // ssh, ssh-agent proxy, and sftp libraries
    compile 'com.jcraft:jsch:0.1.55'
    compile "com.jcraft:jsch.agentproxy.jsch:0.0.9"
    compile "com.jcraft:jsch.agentproxy.connector-factory:0.0.9"
    compile 'com.github.robtimus:sftp-fs:1.2.1'

    // library for easy HTML editing
    compile "org.jsoup:jsoup:1.9.2"

    compile 'com.networknt:json-schema-validator:1.0.65'

    compile "org.grails.plugins:grails-console:2.1.1"
    compile "org.grails.plugins:mail:2.0.0"

    compile "org.grails.plugins:database-migration:3.1.0"
    compile 'org.liquibase:liquibase-core:3.10.3'

    // support for java8 time classes
    compile "org.hibernate:hibernate-java8:5.4.33.Final"

    compile "com.bertramlabs.plugins:asset-pipeline-grails:2.14.10"
    compile "com.bertramlabs.plugins:less-asset-pipeline:2.15.1"

    compile 'org.codenarc:CodeNarc:2.2.0'

    compile 'org.grails.plugins:spring-security-core:4.0.4'
    compile "org.grails.plugins:spring-security-ldap:4.0.0.M1"
    compile "org.grails.plugins:spring-security-acl:4.0.0.M2"
    compile 'org.grails.plugins:spring-security-oauth2-provider:4.0.0-RC1'

    // Grails Defaults
    compile "org.springframework.boot:spring-boot-starter-logging"
    compile "org.springframework.boot:spring-boot-autoconfigure"
    compile "org.grails:grails-core"
    compile "org.springframework.boot:spring-boot-starter-actuator"
    compile "org.springframework.boot:spring-boot-starter-tomcat"
    developmentOnly("org.springframework.boot:spring-boot-devtools")
    compile "org.grails:grails-web-boot"
    compile "org.grails:grails-logging"
    compile "org.grails:grails-plugin-rest"
    compile "org.grails:grails-plugin-databinding"
    compile "org.grails:grails-plugin-i18n"
    compile "org.grails:grails-plugin-services"
    compile "org.grails:grails-plugin-url-mappings"
    compile "org.grails:grails-plugin-interceptors"
    compile "org.grails.plugins:cache:5.0.1"
    compile "org.grails.plugins:async"
    compile "org.grails.plugins:scaffolding"
    compile "org.grails.plugins:events"
    compile "org.grails.plugins:hibernate5:7.0.6"
    compile "org.hibernate:hibernate-core:5.4.33.Final"
    compile "org.grails.plugins:gsp"
    console "org.grails:grails-console"
    profile "org.grails.profiles:web"
    runtime "org.glassfish.web:el-impl:2.1.2-b03"
    runtime "com.h2database:h2"
    runtime "org.apache.tomcat:tomcat-jdbc"
    testCompile "org.grails:grails-gorm-testing-support"
    testCompile "org.grails:grails-web-testing-support"
    workflowTestCompile "org.grails:grails-gorm-testing-support"
    workflowTestCompile "org.grails:grails-web-testing-support"

    // JS and CSS libraries
    compile 'org.webjars:jquery:3.6.0'
    compile 'org.webjars:bootstrap:4.6.0'
    compile 'org.webjars.npm:bootstrap-icons:1.4.0'
    compile 'org.webjars.npm:ttskch__select2-bootstrap4-theme:1.5.2'
    compile 'org.webjars:select2:4.0.13'
    compile 'org.webjars:datatables:1.10.24'
    compile 'org.webjars:datatables-buttons:1.7.0'
    compile 'org.webjars:datatables-fixedcolumns:3.0.2'
    compile 'org.webjars:datatables-fixedheader:2.1.2-1'
    compile 'org.webjars.bowergithub.datatables:dist-datatables-scroller:2.0.1'
    compile 'org.webjars.bowergithub.datatables:dist-datatables-scroller-bootstrap4:2.0.1'
    compile 'org.webjars:datatables-plugins:1.10.24'
    compile 'org.webjars.npm:corejs-typeahead:1.3.1'
    compile 'org.webjars.npm:chartjs-plugin-datalabels:2.1.0'
    compile 'org.webjars:chartjs:3.7.1'
}

task buildInfo {
    def separator = System.getProperty('line.separator')
    def tags = "git tag --points-at HEAD".execute().text.trim()
    def branch = "git branch --show-current".execute().text.trim()
    def commitId = "git log --pretty=format:'%h' -n 1".execute().text.trim()
    mkdir "$buildDir/version"
    def file = new File("$buildDir/version/version.properties")
    file.delete()
    file << "git.tags=$tags$separator"
    file << "git.branch=$branch$separator"
    file << "git.commit.id.abbrev=$commitId$separator"
}

classes.dependsOn buildInfo

bootRun {
    ignoreExitValue true
    jvmArgs('-Dspring.output.ansi.enabled=always')
    sourceResources sourceSets.main
    String springProfilesActive = 'spring.profiles.active'
    systemProperty springProfilesActive, System.getProperty(springProfilesActive)
}

// required for bootRun on Windows
grails {
    pathingJar = true
}

tasks.withType(Test) {
    maxParallelForks = System.getenv('MAX_PARALLEL_FORKS')?.isInteger() ? System.getenv('MAX_PARALLEL_FORKS') as int : 1
    testLogging {
        exceptionFormat = 'full'
        afterSuite { desc, result ->
            if (!desc.parent)
                println("${result.resultType} " +
                        "(${result.testCount} tests, " +
                        "${result.successfulTestCount} successes, " +
                        "${result.failedTestCount} failures, " +
                        "${result.skippedTestCount} skipped)")
        }
    }
}

assets {
    minifyJs = true
    minifyCss = true
    minifyOptions = [
            languageMode  : 'ES6',
            targetLanguage: 'ES5',
    ]
    excludes = ['**/chartjs/**/*', '**/chartjs-plugin-datalabels/**/*']
}

codenarc {
    toolVersion = '2.2.0'
    configFile file('grails-app/conf/CodeNarcRuleSet.groovy')
    maxPriority1Violations = 0
    maxPriority2Violations = 195
    maxPriority3Violations = 2035
}

task showCodenarcHTMLFile(type: Exec) {
    commandLine 'echo', "File can be found under: file://${project.projectDir}/build/reports/codenarc/all.html"
}

task codenarcAll(type: CodeNarc) {
    mustRunAfter clean
    finalizedBy showCodenarcHTMLFile
    reports {
        text.enabled = true
        html.enabled = true
    }
    sourceSets.each { SourceSet s ->
        if (s.name != "script") {
            source(s.allGroovy.srcDirs)
        }
    }

    group = LifecycleBasePlugin.VERIFICATION_GROUP
}

tasks.withType(GroovyCompile) {
    configure(groovyOptions) {
        forkOptions.jvmArgs = ['-Xmx4096m']
    }
}

tasks.withType(CodeNarc) {
    sourceSets.each { SourceSet s ->
        if (s.name != "script") {
            codenarcClasspath += s.runtimeClasspath  // required for ast transformations -> too many dependencies, because of this line we have to
            // set groovy dependency explicitly
            compilationClasspath += s.runtimeClasspath //too many dependencies for codenarc tasks except all
        }
    }
}

war {
    archiveName = 'otp.war'
}

/* Documentation Tasks */

task ensureDocsBuildDirs {
    doLast {
        mkdir "$buildDir/docs/users"
        mkdir "$buildDir/docs/images"
    }
}

task copyCss(type: Copy) {
    from "docs/css"
    into "$buildDir/docs/css"
}

task copyHtml(type: Copy) {
    from "docs/html/"
    into "$buildDir/docs"
}

generateHtml {
    dependsOn(ensureDocsBuildDirs)
    dependsOn(copyHtml)
    dependsOn(copyCss)

    pegdownExtensions = "TABLES, TOC"
    ext.encoding = "UTF-8"
    recursiveInput = true
    inputEncoding = ext.encoding
    outputEncoding = ext.encoding
    inputDirectory = new File("docs/md")
    outputDirectory = new File(buildDir, "docs")

    transformRelativeMarkdownLinks = true
    headerHtmlFile = new File("docs/support/header.html")
    footerHtmlFile = new File("docs/support/footer.html")
}

node {
    version = '14.17.6'
    download = true
}

task esLint(type: NpmTask) {
    dependsOn npmInstall
    args = ['run', 'lint']
}

task esLintExport(type: NpmTask) {
    dependsOn npmInstall
    args = ['run', 'lint-export']
}

task esLintExportCi(type: NpmTask) {
    dependsOn npmInstall
    environment = ['CI': 'true']
    args = ['run', 'lint-export']
}

task esLintAutoFix(type: NpmTask) {
    dependsOn npmInstall
    environment = ['CI':'true']
    args = ['run', 'lint-fix']
}

task runCypressTests(type: NpmTask) {
    doFirst {
        println "Make sure that OTP is running, the connection is configured via environment variable CYPRESS_BASE_URL and credentials are set in cypress.env.json"
    }
    dependsOn npmInstall
    environment = ['CI':'true']
    args = ['run', 'cy:run']
}

task runCypressTestsInWindow(type: NpmTask) {
    doFirst {
        println "Make sure that OTP is running, the connection is configured via environment variable CYPRESS_BASE_URL and credentials are set in cypress.env.json"
    }
    dependsOn npmInstall
    args = ['run', 'cy:open']
}
