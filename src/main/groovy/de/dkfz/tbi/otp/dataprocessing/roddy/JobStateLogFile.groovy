/*
 * Copyright 2011-2019 The OTP authors
 *
 * Permission is hereby granted, free of charge, to any person obtaining a copy
 * of this software and associated documentation files (the "Software"), to deal
 * in the Software without restriction, including without limitation the rights
 * to use, copy, modify, merge, publish, distribute, sublicense, and/or sell
 * copies of the Software, and to permit persons to whom the Software is
 * furnished to do so, subject to the following conditions:
 *
 * The above copyright notice and this permission notice shall be included in all
 * copies or substantial portions of the Software.
 *
 * THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
 * IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
 * FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE
 * AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
 * LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM,
 * OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN THE
 * SOFTWARE.
 */
package de.dkfz.tbi.otp.dataprocessing.roddy

import groovy.transform.*

import de.dkfz.tbi.otp.FileNotFoundException
import de.dkfz.tbi.otp.dataprocessing.ParsingException
import de.dkfz.tbi.otp.ngsdata.FileNotReadableException
import de.dkfz.tbi.otp.utils.WaitingFileUtils

import java.util.regex.Matcher

/**
 * represents a jobStateLogfile.txt file generated by Roddy in
 * roddyExecution directory.
 */
class JobStateLogFile {
    /**
     * the name of jobStateLog file in the roddyExecution directory
     */
    private static final String JOB_STATE_LOG_FILE_NAME = "jobStateLogfile.txt"

    /**
     * each line in the file must satisfy this expression
     * the hostname is optional and will be ignored, it occurs only on PBS systems
     */
    private static final String JOB_STATE_LOGFILE_REGEX =
            /^(?<id>.+?)(\.(?<hostname>.+?))?:(?<statusCode>.+?):(?<timeStamp>.+?):(?<jobClass>.*)$/
    /**
     * represents content of the file; key is the cluster job id, only the latest entry is stored
     */
    private final Map<String, LogFileEntry> logFileEntries

    /**
     * represents JobStateLogFile on the file-system
     */
    private final File file

    /**
     * @param roddyExecutionDirectory subdirectory of
     * {@link de.dkfz.tbi.otp.dataprocessing.RoddyBamFile#getWorkExecutionStoreDirectory} corresponding to a roddy call
     */
    private JobStateLogFile(File roddyExecutionDirectory) {
        file = new File(roddyExecutionDirectory, JOB_STATE_LOG_FILE_NAME)
        validateFile()
        logFileEntries = parseJobStateLogFile().asImmutable()
    }

    static JobStateLogFile getInstance(File roddyExecutionDirectory) {
        return new JobStateLogFile(roddyExecutionDirectory)
    }

    private validateFile() {
        try {
            WaitingFileUtils.waitUntilExists(file)
        } catch (AssertionError ignored) {
            throw new FileNotFoundException("${JOB_STATE_LOG_FILE_NAME} is not found in ${file.parentFile}")
        }
        if (!file.canRead()) {
            throw new FileNotReadableException("$file")
        }
    }

    private Map<String, LogFileEntry> parseJobStateLogFile() {
        Map<String, LogFileEntry> entries = [:]

        file.eachLine { line ->
            Matcher matcher = line =~ JOB_STATE_LOGFILE_REGEX
            if (matcher) {
                LogFileEntry logFileEntry = new LogFileEntry(
                        clusterJobId: matcher.group("id"),
                        statusCode: matcher.group("statusCode"),
                        timeStamp: Long.parseLong(matcher.group("timeStamp")),
                        jobClass: matcher.group("jobClass")
                )

                LogFileEntry existingLoFileEntry = entries[logFileEntry.clusterJobId]
                // only the latest entry is stored (i.e. the entry with the highest timestamp)
                if (!existingLoFileEntry ||
                        logFileEntry.timeStamp > existingLoFileEntry.timeStamp) {
                        entries.put(logFileEntry.clusterJobId, logFileEntry)
                }
            } else {
                throw new ParsingException("${file} contains non-matching entry: ${line}")
            }
        }
        return entries
    }

    boolean containsClusterJobId(String clusterJobId) {
        return logFileEntries.containsKey(clusterJobId)
    }

    boolean isClusterJobFinishedSuccessfully(String clusterJobId) {
        return getPropertyFromLatestLogFileEntry(clusterJobId, "statusCode") == "0"
    }

    String getPropertyFromLatestLogFileEntry(String clusterJobId, String property) {
        return logFileEntries.get(clusterJobId)?."${property}"
    }

    @Immutable
    @EqualsAndHashCode
    @ToString(includeNames = true)
    static class LogFileEntry {
        String clusterJobId
        String statusCode
        long timeStamp
        String jobClass
    }
}
